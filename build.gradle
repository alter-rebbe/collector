plugins {
  id 'com.github.ben-manes.versions'
  id 'idea'
  id 'java'
  id 'io.miret.etienne.sass'
}

final String opentorahVersion = '0.1.58'

description = '19 Kislev Archive'

repositories {
  jcenter()
  mavenCentral()
}

idea {
  module {
    excludeDirs += [
      file('.idea'),
      file('gradle'),
      file('/docs/by'),
      file('/docs/collections'),
      file('/docs/names'),
      file('/docs/notes'),
      file('/docs/reports'),
      file('/docs/*.html'),
      file('gradle')
    ]
  }
}

dependencies {
  runtimeOnly "org.opentorah:opentorah-collector:$opentorahVersion"
}

task generateSite(type: JavaExec) {
  main = 'org.opentorah.collector.Main'
  args = ["$rootDir/docs"]
  classpath = sourceSets.main.runtimeClasspath
}

compileSass {
  sourceDir = project.file ("${projectDir}/src/main/sass")
  outputDir = project.file ("${projectDir}/docs/assets")
}

build.dependsOn(generateSite)
build.dependsOn(compileSass)

task uploadSite(type: JavaExec) {
  main = 'org.opentorah.collector.GoogleCloudStorageSynchronizer'
  args = [
    findProperty('gcloudServiceAccountKey') ?: System.getenv('gcloudServiceAccountKey') ?: "nokey",
    "$rootDir/docs/"
  ]
  classpath = sourceSets.main.runtimeClasspath
}
uploadSite.dependsOn(build)

task localService(type: JavaExec) {
  main = 'org.opentorah.collector.Service'
  environment "STORE", "file://$projectDir/docs/"
  classpath = sourceSets.main.runtimeClasspath
}
localService.dependsOn(build)
